
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    checkstyle {
        configProperties('rootDir': rootDir)
        toolVersion = '8.18'
    }

    group = 'com.hazelcast.jet.contrib'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'                

    dependencies {
        compile 'com.hazelcast.jet:hazelcast-jet:3.0'
        testCompile 'log4j:log4j:1.2.17'
        testCompile 'junit:junit:4.11'
    }

    test {
        systemProperty "hazelcast.phone.home.enabled", "false"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

}

