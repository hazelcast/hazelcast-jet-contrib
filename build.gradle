buildscript {
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
    repositories {
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'maven-publish-auth'
    apply plugin: "maven"

    repositories {
        mavenCentral()
        mavenLocal()
    }

    checkstyle {
        configProperties('rootDir': rootDir)
        toolVersion = '8.18'
    }

    group = 'com.hazelcast.jet.contrib'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        def jetVersion = '3.0'
        def hazelcastVersion = '3.12'
        compile group: 'com.hazelcast.jet', name: 'hazelcast-jet', version: jetVersion
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        testCompile "com.hazelcast.jet:hazelcast-jet-core:" + jetVersion + ":tests"
        testCompile "com.hazelcast:hazelcast:" + hazelcastVersion + ":tests"
        testCompile "com.hazelcast:hazelcast-client:" + hazelcastVersion + ":tests"
        testCompile 'log4j:log4j:1.2.17'
        testCompile 'junit:junit:4.11'
        testCompile 'org.awaitility:awaitility:3.1.6'
    }

    test {
        systemProperty "hazelcast.phone.home.enabled", "false"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            maven {
                name "snapshot-repository"
                url "https://oss.sonatype.org/content/repositories/snapshots"
            }
            mavenLocal()
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

}

